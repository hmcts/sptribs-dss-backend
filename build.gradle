buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://jitpack.io"
      content {
        excludeGroup "com.github.ben-manes.versions"
      }
    }

  }
  dependencies {
    classpath "net.serenity-bdd:serenity-gradle-plugin:2.4.34"
    classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '2.36.1070'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.6.7'
  id 'org.owasp.dependencycheck' version '7.1.0.1'
  id 'com.github.ben-manes.versions' version '0.41.0'
  id 'org.sonarqube' version '3.3'
  id 'idea'
  id 'hmcts.ccd.sdk' version '0.25.16'
  id 'au.com.dius.pact' version '4.1.7'
}

apply plugin: 'cz.habarta.typescript-generator'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

ccd {
  configDir = file('ccd-definitions/definitions')
}

ext['elasticsearch.version'] = '7.17.0'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    //resources.srcDir file('src/contractTest/resources')
  }
  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
  }
}

idea {
  module {
    testSourceDirs += file('src/integrationTest/java')
    testSourceDirs += file('src/smokeTest/java')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom testRuntime

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntime

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntime.extendsFrom testRuntime
  smokeTestCompile.extendsFrom testCompile

  contractTestImplementation.extendsFrom testImplementation
  contractTestRuntime.extendsFrom testRuntime
  contractTestCompile.extendsFrom testCompile
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
  useJUnitPlatform()
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy(aggregate)
  failFast = true
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  setTestClassesDirs(sourceSets.smokeTest.output.classesDirs)
  setClasspath(sourceSets.smokeTest.runtimeClasspath)
  include "uk/gov/hmcts/reform/cosapi/smoketests/**"
  failFast = true
}

task highLevelDataSetup(type: JavaExec) {
  if (System.getenv("ENVIRONMENT") != "prod") {
    mainClass.set("uk.gov.hmcts.reform.cosapi.common.ccd.HighLevelDataSetupApp")
    classpath += sourceSets.main.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  } else {
    println("high level data setup is disabled in prod temporarily")
  }
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.2.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.41.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

jacoco {
  toolVersion = "0.8.5"
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.0
      }
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

def sonarExclusions = [
  '**/uk/gov/hmcts/reform/cosapi/common/**',
  '**/uk/gov/hmcts/reform/cosapi/document/content/DocmosisTemplateConstants.java',
  '**/uk/gov/hmcts/reform/cosapi/document/model/**',
  '**/uk/gov/hmcts/reform/cosapi/edgecase/constants/**',
  '**/uk/gov/hmcts/reform/cosapi/edgecase/model/**',
  '**/uk/gov/hmcts/reform/cosapi/edgecase/search/**',
  '**/uk/gov/hmcts/reform/cosapi/edgecase/task/**',
  '**/uk/gov/hmcts/reform/cosapi/model/PaymentStatus.java',
  '**/uk/gov/hmcts/reform/fis/config/OpenAPIConfiguration.java',
]

sonarqube {
  properties {
    property "sonar.projectName", "sptribs :: sptribs-dss-backend"
    property "sonar.projectKey", "uk.gov.hmcts.reform:sptribs-dss-backend"
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.coverage.exclusions", sonarExclusions.join(', ')
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '31.1-jre') {
      entry 'guava'
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  outputDirectory = 'build/reports/'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
  skipConfigurations = [
    "checkstyle",
    "compileOnly",
    "pmd",
    "integrationTest",
    "functionalTest",
    "smokeTest",
    "contractTestRuntimeClasspath",
    "contractTestCompileClasspath"
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
  jcenter()
}

ext {
  log4JVersion = "2.17.0"
}

def versions = [
  springfoxSwagger  : '3.0.0',
  s2sClient         : '4.0.0',
  springCloudStarterBootstrap : '3.1.1',
  springdocOpenApiUI: '1.6.6',
  comGithubHmctsJavaLogging: '6.0.1',
  jackson: '2.13.2',
  jacksonBom: '2.13.2.20220324',
  reformsJavaLogging: '5.1.7',
  lombok            : '1.18.20',
  junit             : '5.8.2',
  junitPlatform     : '1.8.2',
  awaitility        : '1.7.0',
  springBoot        : '2.6.5',
  serenityCoreVersion: '3.0.2',
  ccdCaseDocumentAmClient      : '1.7.3',
  pact                         : '4.1.7',
  guava                        : '30.1-jre',
  httpComponents         :'4.5.13'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'com.github.hmcts', name: 'ccd-config-generator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  //{
  //exclude  group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310:2.13.2'
  //}
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot
  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-spring-webflux', version: versions.springfoxSwagger
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.17.3-RELEASE'
  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformsJavaLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformsJavaLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.reformsJavaLogging
  implementation group: 'com.github.hmcts', name:'ccd-case-document-am-client', version: versions.ccdCaseDocumentAmClient
  implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '2.0.0'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.8.6'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springdocOpenApiUI

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.comGithubHmctsJavaLogging

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: versions.jacksonBom, ext: 'pom'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.62'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: versions.springCloudStarterBootstrap
  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.s2sClient
  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.10'
  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.3'
  implementation (group: 'com.github.hmcts', name: 'befta-fw', version: '8.7.2-Categories') {
    exclude group: 'org.apache.commons', module: 'commons-compress'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'org.apache.poi', module: 'poi-ooxml'
    exclude group: 'org.apache.poi', module: 'poi'
  }
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.2'
  implementation group: 'org.springframework', name: 'spring-aspects', version: '5.3.19'
  implementation group: 'io.cucumber', name: 'cucumber-java', version: '7.3.3'
  implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
  implementation group: 'org.apache.poi', name: 'poi', version: '5.2.2'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.2'
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloudStarterBootstrap)
    {
      exclude group: 'io.reactivex', module: 'io.reactivex'
      exclude group: 'io.reactivex', module: 'rxnetty'
      exclude group: 'io.reactivex', module: 'rxnetty-contexts'
      exclude group: 'io.reactivex', module: 'rxnetty-servo'
    }

  implementation 'org.projectlombok:lombok:1.18.24'
  compileOnly "org.projectlombok:lombok:1.18.18"
  annotationProcessor "org.projectlombok:lombok:1.18.18"

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation libraries.junit5
  testImplementation(platform('org.junit:junit-bom:5.8.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude(module: 'commons-logging')
    exclude(module: 'android-json')
  }

  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
    exclude(module: 'commons-logging')
    exclude(module: 'android-json')
  }

  testImplementation group: 'io.rest-assured', name: 'rest-assured'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
  testImplementation group: 'org.assertj', name: 'assertj-guava', version: '3.4.0'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-stub-runner', version: '3.0.4'

  testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.3.1'

  testImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: versions.awaitility
  testImplementation group: 'com.github.stefanbirkner', name: 'system-lambda', version: '1.2.0'
  testImplementation group: 'io.github.openfeign', name: 'feign-jackson', version: '12.0'

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'
  functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.s2sClient

  smokeTestCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'

  smokeTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'

  smokeTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  smokeTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitJupiter
  smokeTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitJupiter

  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitJupiter
  smokeTestRuntime "org.junit.platform:junit-platform-commons:1.8.1"

  smokeTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.30') {
    force = true
  }

  testImplementation (group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenityCoreVersion)
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenityCoreVersion)
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenityCoreVersion)
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenityCoreVersion)

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath

  contractTestCompile sourceSets.main.runtimeClasspath
  contractTestCompile sourceSets.test.runtimeClasspath

  contractTestCompile group: 'com.google.guava', name: 'guava', version: versions.guava
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact

  contractTestCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents

  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.8.2"


  contractTestCompile("org.junit.jupiter:junit-jupiter-api:5.8.2")
  contractTestRuntime("org.junit.jupiter:junit-jupiter-engine:5.8.2")
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')

  contractTestCompile sourceSets.main.runtimeClasspath
  contractTestCompile sourceSets.test.runtimeClasspath

}

mainClassName = 'uk.gov.hmcts.reform.cosapi.Application'

ext.getVaultSecret = { String secretName ->
  new ByteArrayOutputStream().withStream { os ->
    exec {
      commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'sptribs-aat', '-o', 'tsv', '--query', 'value', '--name', secretName
      standardOutput = os
    }
    return os.toString().replace('\n', '')
  }
}

bootRun {
  doFirst() {
    if (System.getenv("IDAM_API_BASEURL") == null) {
      println("Loading secrets from vault")

      systemProperty 'IDAM_API_BASEURL', 'https://idam-api.aat.platform.hmcts.net'
      // TODO: Uncomment if required later
      //systemProperty 'UK_GOV_NOTIFY_API_KEY', getVaultSecret('uk-gov-notify-api-key')
      systemProperty 'S2S_SECRET', getVaultSecret('s2s-secret-sptribs-dss-backend')
      //systemProperty 'IDAM_CASEWORKER_USERNAME', getVaultSecret('idam-caseworker-username')
      //systemProperty 'IDAM_CASEWORKER_PASSWORD', getVaultSecret('idam-caseworker-password')
      systemProperty 'IDAM_SYSTEM_UPDATE_USERNAME', getVaultSecret('idam-system-user-name')
      systemProperty 'IDAM_SYSTEM_UPDATE_PASSWORD', getVaultSecret('idam-system-user-password')
      systemProperty 'IDAM_SOLICITOR_USERNAME', getVaultSecret('idam-solicitor-username')
      systemProperty 'IDAM_SOLICITOR_PASSWORD', getVaultSecret('idam-solicitor-password')
      systemProperty 'IDAM_CLIENT_SECRET', getVaultSecret('idam-ui-secret')
      systemProperty 'APP_INSIGHTS_KEY', getVaultSecret('AppInsightsInstrumentationKey')

      if (System.getenv("BOOT_RUN_DOCKER")) {
        systemProperty 'IDAM_API_BASEURL', 'http://localhost:5000'
        systemProperty 'CASE_DATA_STORE_BASEURL', 'http://localhost:4452'
      }
    }
  }
}

bootJar {
  getArchiveFileName().set(provider {
    'sptribs-dss-backend.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

functional {
  doFirst() {
    if (System.getenv("OAUTH2_CLIENT_SECRET") == null) {
      println("Loading secrets from vault")

      systemProperty 'IDAM_API_BASEURL', 'https://idam-api.aat.platform.hmcts.net'
      systemProperty 'UK_GOV_NOTIFY_API_KEY', getVaultSecret('uk-gov-notify-api-key')
      systemProperty 'S2S_SECRET', getVaultSecret('s2s-secret-sptribs-dss-backend')
      //systemProperty 'IDAM_CASEWORKER_USERNAME', getVaultSecret('idam-caseworker-username')
      //systemProperty 'IDAM_CASEWORKER_PASSWORD', getVaultSecret('idam-caseworker-password')
      systemProperty 'IDAM_SYSTEM_UPDATE_USERNAME', getVaultSecret('idam-system-user-name')
      systemProperty 'IDAM_SYSTEM_UPDATE_PASSWORD', getVaultSecret('idam-system-user-password')
      systemProperty 'IDAM_SOLICITOR_USERNAME', getVaultSecret('idam-solicitor-username')
      systemProperty 'IDAM_SOLICITOR_PASSWORD', getVaultSecret('idam-solicitor-password')
      systemProperty 'OAUTH2_CLIENT_SECRET', getVaultSecret('idam-ui-secret')
      systemProperty 'APP_INSIGHTS_KEY', getVaultSecret('AppInsightsInstrumentationKey')
    }
  }
}

generateTypeScript {
  jsonLibrary = 'jackson2'
  classPatterns = [
    'uk.gov.hmcts.reform.cosapi.**.model.*',
    'uk.gov.hmcts.ccd.sdk.type.*',
  ]
  outputFile = 'build/ts/index.ts'
  outputKind = 'module'
  outputFileType = 'implementationFile'
  mapEnum = 'asEnum'
  mapDate = 'asString'
  group = 'ccd tasks'

  doLast {
    exec {
      commandLine 'yarn', 'prepublishOnly'
    }
  }
}

ccd {
  configDir = file('build/definitions')
}

tasks.register('stopCosApiContainer') {
  group = 'ccd tasks'

  doFirst {
    exec {
      executable "sh"
      args "-c", "docker stop sptribs-dss-backend || exit 0"
    }
  }
}

def reloadCcd = tasks.register('reloadCcd') {
  group = 'ccd tasks'
  doLast {
    exec {
      file('../.env').readLines().each() {
        def (key, value) = it.tokenize('=')
        environment key, value
      }
      commandLine './bin/process-and-import-ccd-definition.sh'
    }
    exec {
      file('../.env').readLines().each() {
        def (key, value) = it.tokenize('=')
        environment key, value
      }
      commandLine 'docker-compose', '-f', '../docker-compose.yml', 'up', '--build', '-d', 'sptribs-dss-backend'
    }
  }
}

reloadCcd.configure {
  dependsOn assemble, generateCCDConfig
}

generateCCDConfig.dependsOn stopCosApiContainer

project.ext {
  pacticipant = 'sscs_tribunalsCaseApi'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}
task loadEnvSecrets() {
  doLast {
    if (!project.file('./.aat-env').exists()) {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'sptribs-aat', '-o', 'tsv', '--query', 'value', '--name', 'sptribs-case-api-dot-env'
          standardOutput = os
        }
        project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
      }
    }
  }
}

task buildCCDXlsx(type: Exec, dependsOn: generateCCDConfig) {
  group 'ccd tasks'
  commandLine './bin/ccd-build-definition.sh'
  inputs.dir layout.buildDirectory.dir('definitions')
  outputs.dir layout.buildDirectory.dir('ccd-config')
}

/*bootWithCCD {
  dependsOn(buildCCDXlsx, loadEnvSecrets)
  doFirst() {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment key, value
    }
  }
}*/

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

task contract(type: Test, description: 'Runs the consumer Pact tests.', group: 'Delivery pipeline') {
  useJUnitPlatform()

  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath

  include "uk/gov/hmcts/reform/cosapi/clients/**"
  systemProperty 'pact.rootDir', "pacts"
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task runAndPublishConsumerPactTests(type: Test){
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pactVersion
  }
}
